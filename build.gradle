
// For those who want the bleeding edge
buildscript {
    repositories {
        gradlePluginPortal()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "JitPack"
            url = "https://jitpack.io/"
        }
        maven {
            url = "https://repo.sk1er.club/repository/maven-public"
        }
    }
    dependencies {
        classpath 'com.github.asbyth:ForgeGradle:FG_2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }

}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'


version = "1.2"
group= "com.conutik" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AkumaQoL"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += '--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker'
}

repositories {
    jcenter()
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
    maven {
        url = "https://repo.sk1er.club/repository/maven-public"
    }

    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

configurations {
    // Creates an extra configuration that implements `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom(include)
}


dependencies {
//    include 'club.minnced:java-discord-rpc:2.0.1'
    include('com.github.jagrosh:DiscordIPC:e29d6d8') {
        exclude module: 'log4j'
    }
    include "gg.essential:vigilance-1.8.9-forge:192+mc-1.18"
    annotationProcessor 'org.spongepowered:mixin:0.8.5'
    compileOnly 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    include ('gg.essential:loader-launchwrapper:1.1.3') {
        transitive = false
    }
    compileOnly 'gg.essential:essential-1.8.9-forge:1725'
    compileOnly annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}


processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version':project.version, 'mcversion':project.minecraft.version
            }
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

jar {
    manifest.attributes(
            'ModSide': 'CLIENT',
            'ForceLoadAsMod': true,
            'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker',
            'MixinConfigs': "mixins.helpermod.json"
    )
    enabled = false
}

tasks.reobfJar.dependsOn(tasks.shadowJar)
